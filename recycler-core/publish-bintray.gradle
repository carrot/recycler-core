apply plugin: 'com.jfrog.bintray'

version = libraryVersion
project.archivesBaseName = artifact

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    ext.recyclerViewJar = "${android.sdkDirectory}/extras/android/m2repository/com/android/support/recyclerview-v7/23.0.0/recyclerview-v7-23.0.0-sources.jar"
    ext.annotationJar = "${android.sdkDirectory}/extras/android/m2repository/com/android/support/support-annotations/23.0.0/support-annotations-23.0.0-sources.jar"
    ext.androidSupport_V4 = "${android.sdkDirectory}/extras/android/m2repository/com/android/support/support-v4/23.0.0/support-v4-23.0.0-sources.jar"
    classpath += files(ext.androidJar) +
            files(ext.recyclerViewJar) +
            files(ext.annotationJar) +
            files(ext.androidSupport_V4)

    assert file(ext.androidJar).exists()
    assert file(ext.recyclerViewJar).exists()
    assert file(ext.annotationJar).exists()
    assert file(ext.androidSupport_V4).exists()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                //Determines whether to GPG sign the files. The default is false
                sign = false
                //required if sign is true
                //passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}

